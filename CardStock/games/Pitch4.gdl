;; Pitch
;;
;; https://www.pagat.com/allfours/pitch.html

(game
 
 (setup 
  ;; Set up the players, 4 players, in teams of 2
  (create players 4)
  (create teams (0, 2) (1, 3))
  
  ;; Create the deck source
  (create deck (game iloc STOCK) (deck (RANK (ACE, TWO, THREE, FOUR, FIVE, SIX, SEVEN, 
                                                 EIGHT, NINE, TEN, JACK, QUEEN, KING))
                                       (COLOR (RED (SUIT (HEARTS, DIAMONDS)))
                                              (BLACK (SUIT (CLUBS, SPADES)))))))        
 
 ;; Stages of the game
 (do 
     (
      (shuffle (game iloc STOCK))
      (all player 'P
           (repeat 6
                   (move (top (game iloc STOCK))
                         (top ('P iloc HAND)))))
      (set (game points GAME)
           (
            ((RANK : ACE) 4)
            ((RANK : KING) 3) 
            ((RANK : QUEEN) 2)
            ((RANK : JACK) 1)
            ((RANK : TEN) 10)))

      ))
 
 ;; bidding time!

 ;; players play a round until their cards are gone    
 (stage player
        (end 
         (all player 'P 
              (== (size ('P iloc HAND)) 0)))
                
        ;; players play a hand once
        (stage player
               (end 
                (all player 'P
                     (> (size ('P vloc TRICK)) 0)))
               
               (choice 
                (
                 ;; if first player, play any card, remember it in the lead spot
                 ((== (size (game mem LEAD)) 0)                       
                  (any ((current player) iloc HAND) 'C
                       (do 
                           (
                            (move 'C 
                                  (top ((current player) vloc TRICK)))
                            (remember (top ((current player) vloc TRICK)) 
                                      (top (game mem LEAD)))

                            ;; if first player, then you also establish trump with your first card
                            ((== (size (top (game mem TRUMP))) 0)
                             (remember (top ((current player) vloc TRICK))
                                       (top (game mem TRUMP))))))))

                 ;; if following player and can follow SUIT or play TRUMP
                 ;;   play any card that follows SUIT or TRUMP
                 ((== (size (game mem LEAD)) 1)
                  (any (filter ((current player) iloc HAND) 'L 
                               (or (== (cardatt SUIT 'L)
                                       (cardatt SUIT (top (game mem LEAD))))
                                   (== (cardatt SUIT 'L)
                                       (cardatt SUIT (top (game mem TRUMP))))
                                   ))
                       'C
                       (move 'C 
                             (top ((current player) vloc TRICK)))))
                                                         
                 ;; if following player and cannot follow SUIT or play TRUMP
                 ;;   play any card
                 ((and (== (size (game mem LEAD)) 1)
                       (== (size (filter ((current player) iloc HAND) 'L 
                                         (or (== (cardatt SUIT 'L) 
                                                 (cardatt SUIT (top (game mem LEAD))))
                                             (== (cardatt SUIT 'L)
                                                 (cardatt SUIT (top (game mem TRUMP))))
                       ))) 0))
                  (any ((current player) iloc HAND) 'C
                       (move 'C 
                             (top ((current player) vloc TRICK)))))
                      
            )))
        
        ;; after players play hand, wrap up trick
        (do (
             ;; solidfy card recedence
             (set (game points PRECEDENCE)
                  (
                   ((SUIT : (cardatt SUIT (top (game mem TRUMP)))) 200)
                   ((SUIT : (cardatt SUIT (top (game mem LEAD)))) 100)
                   ((RANK : ACE) 14)
                   ((RANK : KING) 13) 
                   ((RANK : QUEEN) 12)
                   ((RANK : JACK) 11)
                   ((RANK : TEN) 10)
                   ((RANK : NINE) 9)
                   ((RANK : EIGHT) 8)
                   ((RANK : SEVEN) 7)
                   ((RANK : SIX) 6)
                   ((RANK : FIVE) 5)
                   ((RANK : FOUR) 4)
                   ((RANK : THREE) 3)
                   ((RANK : TWO) 2)))
             
             ;; determine who won the hand, set them first next time
             (forget (top (game mem LEAD)))
             (cycle next (owner (max (union (all player 'P 
                                                 ('P vloc TRICK))) using (game points PRECEDENCE))))
 
             ;; count the number of tricks won per team
             (inc ((team (next player)) sto SMUDGE) 1)

             ;; discard all the played cards
             (all player 'P
                  (move (top ('P vloc TRICK)) 
                        (top ((next player) vloc TRICKSWON)))))))
 
 (do 
     (
      (all player 'P
           (do 
               (
                (inc ((team 'P) sto GAMESCORE) (sum ('P vloc TRICKSWON) using (game points GAME)))
                (inc ((team 'P) sto SCORE) 
                     (size (filter ('P vloc TRICKSWON) 'C 
                                   (and (== (cardatt SUIT 'C)
                                            (cardatt SUIT (top (game mem TRUMP))))
                                        (or (== (cardatt RANK 'C)
                                                (cardatt RANK (max (union (all player 'MP ('MP vloc TRICKSWON))))))
                                            (== (cardatt RANK 'C)
                                                (cardatt RANK (min (union (all player 'MP ('MP vloc TRICKSWON))))))
                                            (== (cardatt RANK 'C)
                                                JACK)))))))))))
                
 (stage team
        (end 
         (all player 'P (== (size ('P vloc TRICKSWON)) 0)))
        (do
            (
             ((> ((current team) sto GAMESCORE) ((next team) sto GAMESCORE))
              (inc ((current team) sto SCORE) 1))

            ;; calculate smudge

            ;; determine if bid was made, otherwise set back

             (all (current team) 'TP
                 (repeat all
                     (move (top ('TP vloc TRICKSWON))
                           (top (game vloc DISCARD))))))))
                              
                
 (scoring max ((team (current player)) sto SCORE)))



